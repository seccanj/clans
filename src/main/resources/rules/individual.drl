import org.seccanj.clans.model.Entity;
import org.seccanj.clans.model.Cell;
import org.seccanj.clans.model.RelativeCell;
import org.seccanj.clans.model.World;
import org.seccanj.clans.model.ModelUtils;
import org.seccanj.clans.model.Position;
import org.seccanj.clans.model.Direction;
import org.seccanj.clans.model.Food;
import org.seccanj.clans.model.Direction.Directions;
import org.seccanj.clans.model.entities.Individual;
import org.seccanj.clans.model.entities.Plant;
import org.seccanj.clans.model.control.EndOfTurn;
import org.seccanj.clans.model.control.ActionDone;


rule "Test individual action"
    when
        $individual : Individual( leftActionPoints >= 1, target == null )
        
        $world : World()

    then
        System.out.println("Test action for "+$individual.toString());
        
        modify ( $individual ) {
        	useActionPoints( 1 )
        };

end

rule "Find food"
    when
        $individual : Individual( hasState("hungry"), target == null, $position : position )

        not ActionDone( individual == $individual, actionType == "setTarget" )

        $world : World()
        
		$targetCell: RelativeCell() from $world.scanFirst( "plant", $position, $individual.pickRandomDirections(), 200 )
		
    then
        System.out.println("Setting target for "+$individual.toString());
        
        modify ( $individual ) {
        	setTarget( $targetCell )
        };

        modify ( $individual ) {
        	addState("moving")
        };

		insert(	new ActionDone( $individual, "setTarget" ) );

end

rule "Move to target"
    when
        $individual : Individual(leftActionPoints >= 5, hasState("moving"), target != null, $position : position, $target : target)

        $newPosition : Position() from $target.position
        
    then
        System.out.println("Moving to target ");

        modify ( $individual ) {
        	moveTo( $newPosition, 2 )
        };

        modify ( $individual ) {
        	useActionPoints( 5 )
        };
		
end

rule "Reached target"
    when
        $individual : Individual(hasState("moving"), target != null, position.equals(target.position))

    then
        System.out.println("Reached target");

        modify ( $individual ) {
        	removeState( "moving" )
        };

        modify ( $individual ) {
        	setTarget( null )
        };

end

rule "Eat plant"
    when
        $individual : Individual($position : position)
        
        $adjacentPosition : Position() from ModelUtils.getAdjacentPositions2( $position )
        
        $plant : Plant( position.equals($adjacentPosition), $plantPosition : position )
        
        $world : World()
        
    then
        System.out.println("Eating plant in "+$plantPosition.toString());
        
        modify ( $individual ) {
        	eat( $plant )
        }

        modify ( $individual ) {
        	addEnergy( $plant.energy )
        }

        modify ( $world ) {
        	removeEntity( $plant )
        }

		delete ( $plant )

end

rule "Starvation"
    when
        $individual : Individual( energy < 50 )
        
        exists EndOfTurn( individual == $individual )
        
        not ActionDone( individual == $individual, actionType == "starvation" )

    then
        System.out.println("Starving "+$individual.toString());

        modify ( $individual ) {
        	decreaseHealth( 1 )
        };

        modify ( $individual ) {
        	addState( "hungry" )
        };

		insert(	new ActionDone( $individual, "starvation" ) );

end

rule "Recovering"
    when
        $individual : Individual( health < 100, energy >= 50 )
        
        exists EndOfTurn( individual == $individual )
        
        not ActionDone( individual == $individual, actionType == "recovering" )

    then
        System.out.println("Recovering "+$individual.toString());

        modify ( $individual ) {
        	increaseHealth( 1 )
        };

        modify ( $individual ) {
        	removeState( "hungry" )
        };

		insert(	new ActionDone( $individual, "recovering" ) );

end

rule "Individual end of turn"
    when
        $individual : Individual( leftActionPoints == 0 )
        
        not EndOfTurn( individual == $individual )

    then
        System.out.println("End of turn for "+$individual.toString());

        modify ( $individual ) {
        	useEnergy( 1 )
        };

		insert(	new EndOfTurn( $individual ) );

end

rule "Teardown"
    when
        $individual : Individual( health <= 0 )

        $world : World()

    then
        System.out.println("Removing dead individual " + $individual.toString());
        
        modify ( $world ) {
        	removeEntity( $individual )
        };

        delete ( $individual );

end

query "All Individuals"
    $individual : Individual()
end

query "All Plants"
    $plant : Plant()
end

query "End of turns"
    $endOfTurn : EndOfTurn()
end

query "Action dones"
    $actionDone : ActionDone()
end

