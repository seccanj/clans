package org.seccanj.clans
 
import org.seccanj.clans.model.Entity;
import org.seccanj.clans.model.RelativeCell;
import org.seccanj.clans.model.World;
import org.seccanj.clans.model.ModelUtils
import org.seccanj.clans.model.Position;
import org.seccanj.clans.model.Food;
import org.seccanj.clans.model.Direction.Directions;
import org.seccanj.clans.model.entities.Individual;
import org.seccanj.clans.model.entities.Plant;


rule "Find food"
	ruleflow-group "search"
    when
        $individual : Individual(me == true, target == null, position.row >= 5, position.column >= 5, $position : position)
        
        $world : World()
        
        $targetCell: RelativeCell() from $world.scanFirst( $position, Directions.north, 200 )
        
    then
        System.out.println("Setting target ["+$targetCell.position.row+", "+$targetCell.position.column+"]");
        
        modify ( $individual ) {
        	setTarget( $targetCell )
        }

end

rule "Move north"
	ruleflow-group "movement"
    when
        $individual : Individual(me == true, target == null, position.row >= 5, position.column >= 5, $position : position)
        
        $world : World()
        $newPosition: Position() from ModelUtils.move( $position, Directions.north.d, 2 )
        
    then
        System.out.println("Moving north from "+$position.row+" to "+$newPosition.row);
        
        modify ( $world ) {
        	moveEntity( $position, $newPosition )
        }

        modify ( $individual ) {
        	moveTo( $newPosition )
        }

end

rule "Move to target"
	ruleflow-group "movement"
    when
        $individual : Individual(me == true, target != null, position.row >= 5 && position.row != target.position.row, position.column >= 5 && position.column != target.position.column, $position : position, $targetCell : target)
        
        $world : World()
        $newPosition : Position() from $targetCell.position
        
    then
        System.out.println("Moving to target ["+$newPosition.row+", "+$newPosition.column+"]");
        
        modify ( $world ) {
        	moveEntity( $individual, $newPosition )
        }

        modify ( $individual ) {
        	moveTo( $newPosition )
        }

end

rule "Eat plant"
	ruleflow-group "movement"
    when
        $individual : Individual(me == true, $position : position)
        
        $adjacentPosition : Position() from ModelUtils.getAdjacentPositions2( $position )
                
        $plant : Plant( position.row == $adjacentPosition.row, position.column == $adjacentPosition.column, $plantPosition : position )
        
        $world : World()
        
    then
        System.out.println("Eating plant in "+$plantPosition.row+", "+$plantPosition.column);
        
        modify ( $individual ) {
        	eat( $plant )
        }

        modify ( $world ) {
        	removeEntity( $plant )
        }

		delete ( $plant )

end
